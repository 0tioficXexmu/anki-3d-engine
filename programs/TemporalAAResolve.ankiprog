<!-- 
Copyright (C) 2009-2017, Panagiotis Christopoulos Charitos and contributors.
All rights reserved.
Code licensed under the BSD License.
http://www.anki3d.org/LICENSE
-->
<shaderProgram>
	<shaders>
		<shader type="vert">
			<source><![CDATA[
#include "shaders/QuadVert.glsl"
			]]></source>
		</shader>

		<shader type="frag">
			<source><![CDATA[
#include "shaders/Functions.glsl"
#include "shaders/Pack.glsl"
#include "shaders/Tonemapping.glsl"

// Config
#define VARIANCE_CLIPPING 1
const float VARIANCE_CLIPPING_GAMMA = 1.75;
#define YCBCR 0
const float BLEND_FACTOR = 1.0 / 16.0; // Keep it low to have a better result
#define TONEMAP_FIX 1

layout(location = 0) in vec2 in_uv;

layout(location = 0) out vec3 out_color;

layout(ANKI_TEX_BINDING(0, 0)) uniform sampler2D u_depthRt;
layout(ANKI_TEX_BINDING(0, 1)) uniform sampler2D u_inputRt;
layout(ANKI_TEX_BINDING(0, 2)) uniform sampler2D u_historyRt;

layout(ANKI_UBO_BINDING(0, 0), std140, row_major) uniform u0_
{
	mat4 u_prevViewProjMatMulInvViewProjMat;
};

#if TONEMAP_FIX
#define TONEMAPPING_RESOURCE readonly buffer
#include "shaders/TonemappingResources.glsl"
#endif

#if YCBCR
#define sample(s, uv) rgbToYCbCr(textureLod(s, uv, 0.0).rgb)
#define sampleOffset(s, uv, x, y) rgbToYCbCr(textureLodOffset(s, uv, 0.0, ivec2(x, y)).rgb)
#else
#define sample(s, uv) textureLod(s, uv, 0.0).rgb
#define sampleOffset(s, uv, x, y) textureLodOffset(s, uv, 0.0, ivec2(x, y)).rgb
#endif

void main()
{
	float depth = textureLod(u_depthRt, in_uv, 0.0).r;

	// Get prev uv coords
	vec4 v4 = u_prevViewProjMatMulInvViewProjMat * vec4(UV_TO_NDC(in_uv), depth, 1.0);
	vec2 oldUv = NDC_TO_UV(v4.xy / v4.w);

	// Read textures
	vec3 historyCol = sample(u_historyRt, oldUv);
	vec3 crntCol = sample(u_inputRt, in_uv);

	// Remove ghosting by clamping the history color to neighbour's AABB
	vec3 near0 = sampleOffset(u_inputRt, in_uv, 1, 0);
	vec3 near1 = sampleOffset(u_inputRt, in_uv, 0, 1);
	vec3 near2 = sampleOffset(u_inputRt, in_uv, -1, 0);
	vec3 near3 = sampleOffset(u_inputRt, in_uv, 0, -1);

#if VARIANCE_CLIPPING
	vec3 m1 = crntCol + near0 + near1 + near2 + near3;
	vec3 m2 = crntCol * crntCol + near0 * near0 + near1 * near1 + near2 * near2 + near3 * near3;

	vec3 mu = m1 / 5.0;
	vec3 sigma = sqrt(m2 / 5.0 - mu * mu);

	vec3 boxMin = mu - VARIANCE_CLIPPING_GAMMA * sigma;
	vec3 boxMax = mu + VARIANCE_CLIPPING_GAMMA * sigma;
#else
	vec3 boxMin = min(crntCol, min(near0, min(near1, min(near2, near3))));
	vec3 boxMax = max(crntCol, max(near0, max(near1, max(near2, near3))));
#endif

	historyCol = clamp(historyCol, boxMin, boxMax);

	// Remove jitter (T. Lottes)
#if YCBCR
	float lum0 = crntCol.r;
	float lum1 = historyCol.r;
	float maxLum = boxMax.r;
#elif TONEMAP_FIX
	float lum0 = computeLuminance(tonemap(crntCol, u_exposureThreshold0));
	float lum1 = computeLuminance(tonemap(historyCol, u_exposureThreshold0));
	//float maxLum = computeLuminance(tonemap(boxMax, u_exposureThreshold0));
	float maxLum = 1.0;
#else
	float lum0 = computeLuminance(crntCol);
	float lum1 = computeLuminance(historyCol);
	float maxLum = computeLuminance(boxMax);
#endif

	float diff = abs(lum0 - lum1) / max(lum0, max(lum1, maxLum + EPSILON));
	diff = 1.0 - diff;
	diff = diff * diff;
	float feedback = mix(0.0, BLEND_FACTOR, diff);

	// Write result
#if YCBCR
	out_color = yCbCrToRgb(mix(historyCol, crntCol, feedback));
#else
	out_color = mix(historyCol, crntCol, feedback);
#endif
}
			]]></source>
		</shader>
	</shaders>
</shaderProgram>
