<!-- 
Copyright (C) 2009-2017, Panagiotis Christopoulos Charitos and contributors.
All rights reserved.
Code licensed under the BSD License.
http://www.anki3d.org/LICENSE
-->
<shaderProgram>
	<mutators>
		<mutator name="HORIZONTAL" values="0 1"/>
		<mutator name="KERNEL_SIZE" values="3 5 7 9 11 13 15"/>
		<mutator name="COLOR_COMPONENTS" values="4 3 1"/>
	</mutators>

	<shaders>
		<shader type="vert">
			<source><![CDATA[
#include "shaders/QuadVert.glsl"
			]]></source>
		</shader>

		<shader type="frag">
			<inputs>
				<input name="TEXTURE_SIZE" type="uvec2" const="1"/>
			</inputs>

			<source><![CDATA[
#include "shaders/GaussianBlurCommon.glsl"
#include "shaders/Tonemapping.glsl"

layout(ANKI_TEX_BINDING(0, 0)) uniform sampler2D u_tex; ///< Input FAI

layout(location = 0) in vec2 in_uv;

// Determine color type
#if COLOR_COMPONENTS == 4
#	define COL_TYPE vec4
#	define TEX_FETCH rgba
#elif COLOR_COMPONENTS == 3
#	define COL_TYPE vec3
#	define TEX_FETCH rgb
#elif COLOR_COMPONENTS == 1
#	define COL_TYPE float
#	define TEX_FETCH r
#else
#	error See file
#endif

float computeLumaWeight(float refLuma, COL_TYPE col)
{
	float l = computeLuminance(col);
	float diff = abs(refLuma - l);
	float weight = 1.0 / (EPSILON + diff);
	return weight;
}

// Output
layout(location = 0) out COL_TYPE out_color;

void main()
{
#if HORIZONTAL
	const vec2 TEXEL_SIZE = vec2(1.0 / float(TEXTURE_SIZE.x), 0.0);
#else
	const vec2 TEXEL_SIZE = vec2(0.0, 1.0 / float(TEXTURE_SIZE.y));
#endif

	COL_TYPE col = texture(u_tex, in_uv).TEX_FETCH;
	out_color = col;
	float refLuma = computeLuminance(col);
	float weight = 1.0;
	vec2 texCoordOffset = 1.5 * TEXEL_SIZE;

	for(uint i = 0u; i < STEP_COUNT; ++i)
	{
		COL_TYPE col = texture(u_tex, in_uv + texCoordOffset).TEX_FETCH;
		float w = computeLumaWeight(refLuma, col);
		out_color += col * w;
		weight += w;

		col = texture(u_tex, in_uv - texCoordOffset).TEX_FETCH;
		w = computeLumaWeight(refLuma, col);
		out_color += col * w;
		weight += w;

		texCoordOffset += 2.0 * TEXEL_SIZE;
	}

	out_color = out_color / weight;
}
			]]></source>
		</shader>
	</shaders>
</shaderProgram>
