// Copyright (C) 2009-2020, Panagiotis Christopoulos Charitos and contributors.
// All rights reserved.
// Code licensed under the BSD License.
// http://www.anki3d.org/LICENSE

#pragma anki input const Vec3 diffColor
#pragma anki input const Vec3 specColor
#pragma anki input const F32 roughness
#pragma anki input const F32 metallic
#pragma anki input const Vec3 initialEmission
#pragma anki input const Vec3 finalEmission

#include <shaders/glsl_cpp_common/GpuParticles.h>

layout(push_constant, row_major) uniform pc_
{
	Mat4 u_mvp;
	Vec3 u_minusCameraZ;
	U32 u_particleCount;
};

layout(set = 0, binding = 0) buffer ssbo_
{
	GpuParticle u_particles[];
};

#pragma anki start vert

#include <shaders/Common.glsl>

layout(location = 0) out Vec2 out_velocity;
layout(location = 1) flat out F32 out_lifeFactor;

void main()
{
	const GpuParticle part = u_particles[gl_VertexID / 2];

	const Vec4 crntClipPos = u_mvp * Vec4(part.m_newWorldPosition, 1.0);
	const Vec4 prevClipPos = u_mvp * Vec4(part.m_oldWorldPosition, 1.0);

	gl_Position = ((gl_VertexID & 1) == 0) ? crntClipPos : prevClipPos;

	const Vec2 crntNdc = crntClipPos.xy / crntClipPos.w;
	const Vec2 prevNdc = prevClipPos.xy / prevClipPos.w;

	// It's NDC_TO_UV(prevNdc) - NDC_TO_UV(crntNdc) or:
	out_velocity = ((gl_VertexID & 1) == 0) ? Vec2(0.0) : (prevNdc - crntNdc) * 0.5;

	out_lifeFactor = saturate(1.0 - (part.m_life / part.m_startingLife));
}
#pragma anki end

#pragma anki start frag
#define MAX_EMISSION 1024.0
#include <shaders/Pack.glsl>

layout(location = 0) out Vec4 out_gbuffer0;
layout(location = 1) out Vec4 out_gbuffer1;
layout(location = 2) out Vec4 out_gbuffer2;
layout(location = 3) out Vec2 out_gbuffer3;

layout(location = 0) in Vec2 in_velocity;
layout(location = 1) flat in F32 in_lifeFactor;

void main()
{
	GbufferInfo g;
	g.m_diffuse = diffColor;
	g.m_normal = u_minusCameraZ;
	g.m_specular = specColor;
	g.m_roughness = roughness;
	g.m_subsurface = 0.0;
	const Vec3 emission = mix(initialEmission, finalEmission, in_lifeFactor);
	g.m_emission = (emission.r + emission.g + emission.b) / 3.0;
	g.m_metallic = metallic;
	g.m_velocity = in_velocity;
	writeGBuffer(g, out_gbuffer0, out_gbuffer1, out_gbuffer2, out_gbuffer3);
}
#pragma anki end
